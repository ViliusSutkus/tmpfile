version = '1.0.1'
group = 'com.viliussutkus89'

apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 29

        versionCode 3
        versionName project.version

        archivesBaseName = rootProject.name
        ndk.abiFilters = project.hasProperty('abi') ? [ project.property('abi') ] : null
    }
    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
        }
    }
    ndkVersion "20.0.5594570"
}

dependencies {
    implementation 'androidx.annotation:annotation:1.1.0'
}

task sourceJar(type: Jar) {
    into ("", { from android.sourceSets.main.java.srcDirs} )
    into ("cpp", { from fileTree(dir: "src/main/cpp/") } )
    archiveBaseName = rootProject.name
    archiveClassifier = 'sources'
}

task javadoc(type: Javadoc) {
    options.encoding = 'UTF-8'
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    title = "${rootProject.name} ${project.version} API"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')

    publications = ['projectRelease']

    pkg {
        repo = 'maven-repo'
        name = rootProject.name
        desc = 'tmpfile function overload for broken implementations'

        websiteUrl = 'https://github.com/ViliusSutkus89/tmpfile-Android'
        vcsUrl = 'https://github.com/ViliusSutkus89/tmpfile-Android.git'
        issueTrackerUrl = 'https://github.com/ViliusSutkus89/tmpfile-Android/issues'

        licenses = ['GPL-3.0-only']
        labels = ['tmpfile', 'android', 'workaround', 'posix', 'bionic']

        publish = true

        version {
            name = this.version
            released = new Date()
            vcsTag = "v${this.version}"
        }
    }
}

publishing {
    publications {
        projectRelease(MavenPublication) {
            artifactId rootProject.name
            artifact "$buildDir/outputs/aar/${rootProject.name}-release.aar"
            artifact sourceJar
            artifact javadocJar
            pom {
                packaging = 'aar'
                url = 'https://github.com/ViliusSutkus89/tmpfile-Android'
                inceptionYear = '2019'
                licenses {
                    license {
                        name = 'The GNU General Public License, Version 3'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.txt'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = "ViliusSutkus89"
                        name = "Vilius Sutkus"
                        email = "ViliusSutkus89@gmail.com"
                    }
                }
                scm {
                    url = 'https://github.com/ViliusSutkus89/tmpfile-Android'
                    connection = 'https://github.com/ViliusSutkus89/tmpfile-Android.git'
                }
            }
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included in the pom.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}
