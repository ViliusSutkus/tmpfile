plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'signing'
}

group = 'com.viliussutkus89'

android {
    namespace = "com.viliussutkus89.android.tmpfile"
    compileSdk 35
    defaultConfig {
        minSdkVersion 16
        targetSdk 35

        versionCode 220
        versionName rootProject.version

        archivesBaseName = rootProject.name

        consumerProguardFiles 'proguard-rules.pro'
    }
    ndkVersion "23.1.7779620"
    externalNativeBuild.cmake.path "src/main/cpp/CMakeLists.txt"
    defaultConfig.externalNativeBuild.cmake.arguments "-DANDROID_STL=none"
    packagingOptions.exclude("**libtmpfile.so")

    buildFeatures.prefabPublishing true
    prefab {
        tmpfile.headers "src/main/cpp/include"
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    implementation 'androidx.annotation:annotation:1.8.2'
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                artifactId rootProject.name
                version = rootProject.version

                pom {
                    name = rootProject.name
                    description = 'tmpfile function overload for broken implementations'
                    url = 'https://github.com/ViliusSutkus89/tmpfile-Android'
                    packaging = 'aar'
                    inceptionYear = '2019'
                    licenses {
                        license {
                            name = 'The GNU General Public License, Version 3'
                            url = 'https://www.gnu.org/licenses/gpl-3.0.txt'
                            distribution = 'repo'
                        }
                    }
                    developers {
                        developer {
                            id = "ViliusSutkus89"
                            name = "Vilius Sutkus"
                            email = "ViliusSutkus89@gmail.com"
                        }
                    }
                    scm {
                        url = 'https://github.com/ViliusSutkus89/tmpfile-Android'
                        connection = 'https://github.com/ViliusSutkus89/tmpfile-Android.git'
                    }
                }
            }
        }
    }

    // This may look ugly...
    // but useInMemoryPgpKeys expects valid keys even with required { false }
    if (System.getenv('SIGNING_KEY')) {
        signing {
            required { true }
            useInMemoryPgpKeys(System.getenv('SIGNING_KEY'), System.getenv('SIGNING_PASS'))
            sign publishing.publications.release
        }
    }
}

tasks.named("clean") {
    def dotCxxDir = layout.projectDirectory.dir(".cxx")
    doFirst {
        delete dotCxxDir
    }
}
