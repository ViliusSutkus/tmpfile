apply plugin: 'com.android.application'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"
    defaultConfig {
        applicationId "com.viliussutkus89.tmpfile.sampleapp"
        minSdkVersion 16
        targetSdkVersion 29
        versionCode 10
        versionName '1.1.4'
    }
    externalNativeBuild.cmake.path "src/main/cpp/CMakeLists.txt"
    ndkVersion "22.0.7026061"

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation 'com.viliussutkus89:tmpfile-android:1.1.4'

    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
}

// Extract shared .so libraries to build directory
// So that they could be linked against in CMake
task extractLibtmpfileSoForLinkingInCMake {
    doLast {
        def tmpfileandroid = configurations.releaseRuntimeClasspath.getResolvedConfiguration().getResolvedArtifacts().find {
            it.name.startsWith("tmpfile-android")
        }
        copy {
            from zipTree(tmpfileandroid.getFile())
            into "${project.buildDir}/tmpfile/"
            include "jni/**/libtmpfile.so"
        }
    }
}

preBuild.dependsOn extractLibtmpfileSoForLinkingInCMake
