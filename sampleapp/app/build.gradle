apply plugin: 'com.android.application'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"
    defaultConfig {
        applicationId "com.viliussutkus89.tmpfile.sampleapp"
        minSdkVersion 16
        targetSdkVersion 29
        versionCode 3
        versionName '1.0.1'
    }
    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
        }
    }
    ndkVersion "20.1.5948944"
}

dependencies {
    implementation 'com.viliussutkus89:tmpfile-android:1.0.1'

    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
}

// Extract shared .so libraries to build directory
// So that they could be linked against in CMake
task extractLibtmpfileSoForLinkingInCMake {
  doLast {
    def tmpfileandroid = configurations.archives.find { it.name.startsWith("tmpfile-android") }
    copy {
      from zipTree(tmpfileandroid.getPath())
      into "${project.buildDir}/tmpfile/"
      include "jni/**/libtmpfile.so"
    }
  }
}

preBuild.dependsOn extractLibtmpfileSoForLinkingInCMake
