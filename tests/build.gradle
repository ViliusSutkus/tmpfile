apply plugin: 'com.android.library'

android {
    namespace = "com.viliussutkus89.android.tmpfile.tests"
    compileSdk 35
    defaultConfig {
        minSdkVersion 16
        targetSdk 35
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    externalNativeBuild.cmake.path "src/androidTest/cpp/CMakeLists.txt"
    ndkVersion "23.1.7779620"
    buildFeatures.prefab true
}

tasks.whenTaskAdded {
    switch (it.name) {
    case "preDebugBuild":
        it.dependsOn ":tmpfile:prefabDebugConfigurePackage"
        break;
    case "preReleaseBuild":
        it.dependsOn ":tmpfile:prefabReleaseConfigurePackage"
        break;
    }
}

dependencies {
    implementation project(":tmpfile")
    testImplementation 'junit:junit:4.13.2'
    // androidx.test.ext:junit:1.1.5 requires minSdk 19
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    // androidx.test.espresso:espresso-core:3.6.0 requires minSdk 19
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

tasks.named("clean") {
    def dotCxxDir = layout.projectDirectory.dir(".cxx")
    doFirst {
        delete dotCxxDir
    }
}
